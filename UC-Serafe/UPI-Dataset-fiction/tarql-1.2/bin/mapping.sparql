PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX cv: <http://data.europa.eu/m8g/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
# Prefixes to shorten the generated turtle file
PREFIX person: <http://www.w3.org/ns/person#>
PREFIX example-person: <http://example.com/person/>
PREFIX example-avs-id: <http://example.com/avs_id/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX human-sex: <http://publications.europa.eu/resource/authority/human-sex/>
PREFIX opendataswiss-org: <https://register.ld.admin.ch/opendataswiss/org/>

CONSTRUCT {
	# 3 Triples to define the class of pre-defined concepts
	# 	needed for the SHACL validation
	human-sex:FEMALE a skos:Concept .
	human-sex:MALE a skos:Concept .
	opendataswiss-org:bundesamt-fur-sozialversicherungen-bsv a foaf:Agent .

	?pers a person:Person ;
			foaf:familyName ?familyNameFr ;
			foaf:givenName ?givenNameFr ;
			cv:birthDate ?birthDateAsDate ;
			cv:sex ?sexURL ;
			rdfs:seeAlso ?wikidataURL ;
			dct:identifier ?avsIdentifierURL .

	?avsIdentifierURL a adms:Identifier ;
			adms:schemeAgency "Bundesamt f√ºr Sozialversicherungen BSV"@de ;
			rdfs:label "AHV-Nummer"@de ;
			dct:creator opendataswiss-org:bundesamt-fur-sozialversicherungen-bsv ;
			skos:notation ?AVSNrStr .
}
WHERE {
  BIND (URI(CONCAT('http://example.com/person/', STR(?AVSNr))) AS ?pers)
  BIND (URI(CONCAT('http://example.com/avs_id/', STR(?AVSNr))) AS ?avsIdentifierURL)
  BIND (URI(CONCAT('http://www.wikidata.org/entity/', STR(?WikidataURL))) AS ?wikidataURL)
  BIND (STR(?AVSNr) as ?AVSNrStr)
  BIND (xsd:date(?birthDate) as ?birthDateAsDate)
  BIND (URI(IF(?sex = "male", "http://publications.europa.eu/resource/authority/human-sex/MALE", "http://publications.europa.eu/resource/authority/human-sex/FEMALE")) as ?sexURL)
  BIND(STRLANG(?familyName, "de") as ?familyNameFr)
  BIND(STRLANG(?givenName, "de") as ?givenNameFr)
  }
